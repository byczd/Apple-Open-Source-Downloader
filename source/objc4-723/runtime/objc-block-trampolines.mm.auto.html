<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<title>objc-block-trampolines.mm</title>
<style type="text/css">
.enscript-comment { font-style: italic; color: rgb(178,34,34); }
.enscript-function-name { font-weight: bold; color: rgb(0,0,255); }
.enscript-variable-name { font-weight: bold; color: rgb(184,134,11); }
.enscript-keyword { font-weight: bold; color: rgb(160,32,240); }
.enscript-reference { font-weight: bold; color: rgb(95,158,160); }
.enscript-string { font-weight: bold; color: rgb(188,143,143); }
.enscript-builtin { font-weight: bold; color: rgb(218,112,214); }
.enscript-type { font-weight: bold; color: rgb(34,139,34); }
.enscript-highlight { text-decoration: underline; color: 0; }
</style>
</head>
<body id="top">
<h1 style="margin:8px;" id="f1">objc-block-trampolines.mm&nbsp;&nbsp;&nbsp;<span style="font-weight: normal; font-size: 0.5em;">[<a href="objc-block-trampolines.mm">plain text</a>]</span></h1>
<hr/>
<div></div>
<pre>
/*
 * Copyright (c) 2010 Apple Inc.  All Rights Reserved.
 * 
 * @APPLE_LICENSE_HEADER_START@
 * 
 * This file contains Original Code and/or Modifications of Original Code
 * as defined in and that are subject to the Apple Public Source License
 * Version 2.0 (the 'License'). You may not use this file except in
 * compliance with the License. Please obtain a copy of the License at
 * <a href="http://www.opensource.apple.com/apsl/">http://www.opensource.apple.com/apsl/</a> and read it before using this
 * file.
 * 
 * The Original Code and all software distributed under the License are
 * distributed on an 'AS IS' basis, WITHOUT WARRANTY OF ANY KIND, EITHER
 * EXPRESS OR IMPLIED, AND APPLE HEREBY DISCLAIMS ALL SUCH WARRANTIES,
 * INCLUDING WITHOUT LIMITATION, ANY WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE, QUIET ENJOYMENT OR NON-INFRINGEMENT.
 * Please see the License for the specific language governing rights and
 * limitations under the License.
 * 
 * @APPLE_LICENSE_HEADER_END@
 */
/***********************************************************************
 * objc-block-trampolines.m
 * Author:	b.bum
 *
 **********************************************************************/

/***********************************************************************
 * Imports.
 **********************************************************************/
#include &quot;objc-private.h&quot;
#include &quot;runtime.h&quot;

#include &lt;Block.h&gt;
#include &lt;Block_private.h&gt;
#include &lt;mach/mach.h&gt;

// symbols defined in assembly files
// Don't use the symbols directly; they're thumb-biased on some ARM archs.
#define TRAMP(tramp)                                \
    static inline __unused uintptr_t tramp(void) {  \
        extern void *_##tramp;                      \
        return ((uintptr_t)&amp;_##tramp) &amp; ~1UL;       \
    }
// Scalar return
TRAMP(a1a2_tramphead);   // trampoline header code
TRAMP(a1a2_firsttramp);  // first trampoline
TRAMP(a1a2_trampend);    // after the last trampoline

#if SUPPORT_STRET
// Struct return
TRAMP(a2a3_tramphead);
TRAMP(a2a3_firsttramp);
TRAMP(a2a3_trampend);
#endif

// argument mode identifier
typedef enum {
    ReturnValueInRegisterArgumentMode,
#if SUPPORT_STRET
    ReturnValueOnStackArgumentMode,
#endif
    
    ArgumentModeCount
} ArgumentMode;


// We must take care with our data layout on architectures that support 
// multiple page sizes.
// 
// The trampoline template in __TEXT is sized and aligned with PAGE_MAX_SIZE.
// On some platforms this requires additional linker flags.
// 
// When we allocate a page pair, we use PAGE_MAX_SIZE size. 
// This allows trampoline code to find its data by subtracting PAGE_MAX_SIZE.
// 
// When we allocate a page pair, we use the process's page alignment. 
// This simplifies allocation because we don't need to force greater than 
// default alignment when running with small pages, but it also means 
// the trampoline code MUST NOT look for its data by masking with PAGE_MAX_MASK.

struct TrampolineBlockPagePair 
{
    TrampolineBlockPagePair *nextPagePair; // linked list of all pages
    TrampolineBlockPagePair *nextAvailablePage; // linked list of pages with available slots
    
    uintptr_t nextAvailable; // index of next available slot, endIndex() if no more available
    
    // Payload data: block pointers and free list.
    // Bytes parallel with trampoline header code are the fields above or unused
    // uint8_t blocks[ PAGE_MAX_SIZE - sizeof(TrampolineBlockPagePair) ] 
    
    // Code: trampoline header followed by trampolines.
    // uint8_t trampolines[PAGE_MAX_SIZE];
    
    // Per-trampoline block data format:
    // initial value is 0 while page data is filled sequentially 
    // when filled, value is reference to Block_copy()d block
    // when empty, value is index of next available slot OR 0 if never used yet
    
    union Payload {
        id block;
        uintptr_t nextAvailable;  // free list
    };
    
    static uintptr_t headerSize() {
        return (uintptr_t) (a1a2_firsttramp() - a1a2_tramphead());
    }
    
    static uintptr_t slotSize() {
        return 8;
    }

    static uintptr_t startIndex() {
        // headerSize is assumed to be slot-aligned
        return headerSize() / slotSize();
    }

    static uintptr_t endIndex() {
        return (uintptr_t)PAGE_MAX_SIZE / slotSize();
    }

    static bool validIndex(uintptr_t index) {
        return (index &gt;= startIndex() &amp;&amp; index &lt; endIndex());
    }

    Payload *payload(uintptr_t index) {
        assert(validIndex(index));
        return (Payload *)((char *)this + index*slotSize());
    }

    IMP trampoline(uintptr_t index) {
        assert(validIndex(index));
        char *imp = (char *)this + index*slotSize() + PAGE_MAX_SIZE;
#if __arm__
        imp++;  // trampoline is Thumb instructions
#endif
        return (IMP)imp;
    }

    uintptr_t indexForTrampoline(IMP tramp) {
        uintptr_t tramp0 = (uintptr_t)this + PAGE_MAX_SIZE;
        uintptr_t start = tramp0 + headerSize();
        uintptr_t end = tramp0 + PAGE_MAX_SIZE;
        uintptr_t address = (uintptr_t)tramp;
        if (address &gt;= start  &amp;&amp;  address &lt; end) {
            return (uintptr_t)(address - tramp0) / slotSize();
        }
        return 0;
    }

    static void check() {
        assert(TrampolineBlockPagePair::slotSize() == 8);
        assert(TrampolineBlockPagePair::headerSize() &gt;= sizeof(TrampolineBlockPagePair));
        assert(TrampolineBlockPagePair::headerSize() % TrampolineBlockPagePair::slotSize() == 0);
        
        // _objc_inform(&quot;%p %p %p&quot;, a1a2_tramphead(), a1a2_firsttramp(), 
        // a1a2_trampend());
        assert(a1a2_tramphead() % PAGE_SIZE == 0);  // not PAGE_MAX_SIZE
        assert(a1a2_tramphead() + PAGE_MAX_SIZE == a1a2_trampend());
#if SUPPORT_STRET
        // _objc_inform(&quot;%p %p %p&quot;, a2a3_tramphead(), a2a3_firsttramp(), 
        // a2a3_trampend());
        assert(a2a3_tramphead() % PAGE_SIZE == 0);  // not PAGE_MAX_SIZE
        assert(a2a3_tramphead() + PAGE_MAX_SIZE == a2a3_trampend());
#endif
        
#if __arm__
        // make sure trampolines are Thumb
        extern void *_a1a2_firsttramp;
        extern void *_a2a3_firsttramp;
        assert(((uintptr_t)&amp;_a1a2_firsttramp) % 2 == 1);
        assert(((uintptr_t)&amp;_a2a3_firsttramp) % 2 == 1);
#endif
    }

};

// two sets of trampoline pages; one for stack returns and one for register returns
static TrampolineBlockPagePair *headPagePairs[ArgumentModeCount];

#pragma mark Utility Functions

static inline void _lock() {
#if __OBJC2__
    runtimeLock.write();
#else
    classLock.lock();
#endif
}

static inline void _unlock() {
#if __OBJC2__
    runtimeLock.unlockWrite();
#else
    classLock.unlock();
#endif
}

static inline void _assert_locked() {
#if __OBJC2__
    runtimeLock.assertWriting();
#else
    classLock.assertLocked();
#endif
}

#pragma mark Trampoline Management Functions
static TrampolineBlockPagePair *_allocateTrampolinesAndData(ArgumentMode aMode) 
{
    _assert_locked();

    vm_address_t dataAddress;
    
    TrampolineBlockPagePair::check();

    TrampolineBlockPagePair *headPagePair = headPagePairs[aMode];
    
    assert(headPagePair == nil  ||  headPagePair-&gt;nextAvailablePage == nil);
    
    kern_return_t result;
    result = vm_allocate(mach_task_self(), &amp;dataAddress, PAGE_MAX_SIZE * 2,
                         VM_FLAGS_ANYWHERE | VM_MAKE_TAG(VM_MEMORY_FOUNDATION));
    if (result != KERN_SUCCESS) {
        _objc_fatal(&quot;vm_allocate trampolines failed (%d)&quot;, result);
    }

    vm_address_t codeAddress = dataAddress + PAGE_MAX_SIZE;
        
    uintptr_t codePage;
    switch(aMode) {
    case ReturnValueInRegisterArgumentMode:
        codePage = a1a2_tramphead();
        break;
#if SUPPORT_STRET
    case ReturnValueOnStackArgumentMode:
        codePage = a2a3_tramphead();
        break;
#endif
    default:
        _objc_fatal(&quot;unknown return mode %d&quot;, (int)aMode);
        break;
    }
    
    vm_prot_t currentProtection, maxProtection;
    result = vm_remap(mach_task_self(), &amp;codeAddress, PAGE_MAX_SIZE, 
                      0, VM_FLAGS_FIXED | VM_FLAGS_OVERWRITE,
                      mach_task_self(), codePage, TRUE, 
                      &amp;currentProtection, &amp;maxProtection, VM_INHERIT_SHARE);
    if (result != KERN_SUCCESS) {
        // vm_deallocate(mach_task_self(), dataAddress, PAGE_MAX_SIZE * 2);
        _objc_fatal(&quot;vm_remap trampolines failed (%d)&quot;, result);
    }
    
    TrampolineBlockPagePair *pagePair = (TrampolineBlockPagePair *) dataAddress;
    pagePair-&gt;nextAvailable = pagePair-&gt;startIndex();
    pagePair-&gt;nextPagePair = nil;
    pagePair-&gt;nextAvailablePage = nil;
    
    if (headPagePair) {
        TrampolineBlockPagePair *lastPagePair = headPagePair;
        while(lastPagePair-&gt;nextPagePair) {
            lastPagePair = lastPagePair-&gt;nextPagePair;
        }
        lastPagePair-&gt;nextPagePair = pagePair;
        headPagePairs[aMode]-&gt;nextAvailablePage = pagePair;
    } else {
        headPagePairs[aMode] = pagePair;
    }
    
    return pagePair;
}

static TrampolineBlockPagePair *
_getOrAllocatePagePairWithNextAvailable(ArgumentMode aMode) 
{
    _assert_locked();
    
    TrampolineBlockPagePair *headPagePair = headPagePairs[aMode];

    if (!headPagePair)
        return _allocateTrampolinesAndData(aMode);
    
    // make sure head page is filled first
    if (headPagePair-&gt;nextAvailable != headPagePair-&gt;endIndex())
        return headPagePair;
    
    if (headPagePair-&gt;nextAvailablePage) // check if there is a page w/a hole
        return headPagePair-&gt;nextAvailablePage;
    
    return _allocateTrampolinesAndData(aMode); // tack on a new one
}

static TrampolineBlockPagePair *
_pageAndIndexContainingIMP(IMP anImp, uintptr_t *outIndex, 
                           TrampolineBlockPagePair **outHeadPagePair) 
{
    _assert_locked();

    for (int arg = 0; arg &lt; ArgumentModeCount; arg++) {
        for (TrampolineBlockPagePair *pagePair = headPagePairs[arg]; 
             pagePair;
             pagePair = pagePair-&gt;nextPagePair)
        {
            uintptr_t index = pagePair-&gt;indexForTrampoline(anImp);
            if (index) {
                if (outIndex) *outIndex = index;
                if (outHeadPagePair) *outHeadPagePair = headPagePairs[arg];
                return pagePair;
            }
        }
    }
    
    return nil;
}


static ArgumentMode 
_argumentModeForBlock(id block) 
{
    ArgumentMode aMode = ReturnValueInRegisterArgumentMode;

#if SUPPORT_STRET
    if (_Block_has_signature(block) &amp;&amp; _Block_use_stret(block))
        aMode = ReturnValueOnStackArgumentMode;
#else
    assert(! (_Block_has_signature(block) &amp;&amp; _Block_use_stret(block)));
#endif
    
    return aMode;
}


// `block` must already have been copied 
IMP 
_imp_implementationWithBlockNoCopy(id block)
{
    _assert_locked();

    ArgumentMode aMode = _argumentModeForBlock(block);

    TrampolineBlockPagePair *pagePair = 
        _getOrAllocatePagePairWithNextAvailable(aMode);
    if (!headPagePairs[aMode])
        headPagePairs[aMode] = pagePair;

    uintptr_t index = pagePair-&gt;nextAvailable;
    assert(index &gt;= pagePair-&gt;startIndex()  &amp;&amp;  index &lt; pagePair-&gt;endIndex());
    TrampolineBlockPagePair::Payload *payload = pagePair-&gt;payload(index);
    
    uintptr_t nextAvailableIndex = payload-&gt;nextAvailable;
    if (nextAvailableIndex == 0) {
        // First time through (unused slots are zero). Fill sequentially.
        // If the page is now full this will now be endIndex(), handled below.
        nextAvailableIndex = index + 1;
    }
    pagePair-&gt;nextAvailable = nextAvailableIndex;
    if (nextAvailableIndex == pagePair-&gt;endIndex()) {
        // PagePair is now full (free list or wilderness exhausted)
        // Remove from available page linked list
        TrampolineBlockPagePair *iterator = headPagePairs[aMode];
        while(iterator &amp;&amp; (iterator-&gt;nextAvailablePage != pagePair)) {
            iterator = iterator-&gt;nextAvailablePage;
        }
        if (iterator) {
            iterator-&gt;nextAvailablePage = pagePair-&gt;nextAvailablePage;
            pagePair-&gt;nextAvailablePage = nil;
        }
    }
    
    payload-&gt;block = block;
    return pagePair-&gt;trampoline(index);
}


#pragma mark Public API
IMP imp_implementationWithBlock(id block) 
{
    block = Block_copy(block);
    _lock();
    IMP returnIMP = _imp_implementationWithBlockNoCopy(block);
    _unlock();
    return returnIMP;
}


id imp_getBlock(IMP anImp) {
    uintptr_t index;
    TrampolineBlockPagePair *pagePair;
    
    if (!anImp) return nil;
    
    _lock();
    
    pagePair = _pageAndIndexContainingIMP(anImp, &amp;index, nil);
    
    if (!pagePair) {
        _unlock();
        return nil;
    }

    TrampolineBlockPagePair::Payload *payload = pagePair-&gt;payload(index);
    
    if (payload-&gt;nextAvailable &lt;= TrampolineBlockPagePair::endIndex()) {
        // unallocated
        _unlock();
        return nil;
    }
    
    _unlock();
    
    return payload-&gt;block;
}

BOOL imp_removeBlock(IMP anImp) {
    TrampolineBlockPagePair *pagePair;
    TrampolineBlockPagePair *headPagePair;
    uintptr_t index;
    
    if (!anImp) return NO;
    
    _lock();
    pagePair = _pageAndIndexContainingIMP(anImp, &amp;index, &amp;headPagePair);
    
    if (!pagePair) {
        _unlock();
        return NO;
    }

    TrampolineBlockPagePair::Payload *payload = pagePair-&gt;payload(index);
    id block = payload-&gt;block;
    // block is released below
    
    payload-&gt;nextAvailable = pagePair-&gt;nextAvailable;
    pagePair-&gt;nextAvailable = index;
    
    // make sure this page is on available linked list
    TrampolineBlockPagePair *pagePairIterator = headPagePair;
    
    // see if page is the next available page for any existing pages
    while (pagePairIterator-&gt;nextAvailablePage &amp;&amp; 
           pagePairIterator-&gt;nextAvailablePage != pagePair)
    {
        pagePairIterator = pagePairIterator-&gt;nextAvailablePage;
    }
    
    if (! pagePairIterator-&gt;nextAvailablePage) {
        // if iteration stopped because nextAvail was nil
        // add to end of list.
        pagePairIterator-&gt;nextAvailablePage = pagePair;
        pagePair-&gt;nextAvailablePage = nil;
    }
    
    _unlock();
    Block_release(block);
    return YES;
}
</pre>
<hr />
</body></html>